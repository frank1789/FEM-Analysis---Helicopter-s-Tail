/COM, ---------------------------------------------------------------------------------
/COM, ROTOR DISK MODAL & TRANSIENT ANALISYS
/COM, ---------------------------------------------------------------------------------
/COM, cantilevered disk-spindle system
/COM, ---------------------------------------------------------------------------------

FINISH
/CLEAR, START, NEW
/FILNAM, Tail_Rotor_disk
/TITLE, Tail rotor disk
*USE,testperson.mac

! >>>>> MODEL PARAMETERS <<<<<
shftlen = 0.50
shftdia = 0.05
dskoff = shftlen
brngoff = 0.12
dskdia = 1.912/2
d_i = 1.75/2
dskthk = 0.15
bstf = 378e+7 ! Brng Stiffness
nmd = 10      ! Number of modes
nmspn = 8     ! Number of speeds
mxspn = 2000  ! Max Omega (rpm)


/PREP7
! >>>>> MATERIAL PROPERTIES <<<<<
! Aluminum 7075
MP,EX,1,71.7e+9
MP,DENS,1,2810
MP,NUXY,1,0.33

! Build model nodes
N,1
N,8,brngoff
FILL,1,8
N,19,dskoff
FILL,8,19
N,20,nx(19) + 0.02


! Bearing ground nodes
N,101,
N,201,
N,110,NX(8)
N,210,NX(8)

! Use 188 elements, solid
ET,1,BEAM188,,,2
SECTYPE,1,BEAM,CSOLID
SECDATA,shftdia/2
! Make elements
SECNUM,1
TYPE,1 
MAT,1
REAL,1
E,1,2
EGEN,18,1,1

! Disk
CSYS,0
WPOFFS, NX(20), NY(20), NZ(20)
WPROTA,,,90
/VIEW,1,1,1,1
CYL4,0,0,d_i/2,0,dskdia/2,360


ET,2,SHELL181
SECTYPE,2,SHELL
SECD,dskthk
TYPE,2
SECN,2
MAT,1
MSHKEY,0
ESIZE,0.02
AMESH,ALL


! ** clamp between disk and spindle
ET,3,MASS21
R,3,10,10,10,8.59E-2,4.295E-2,4.295E-2
TYPE,3
REAL,3
MAT,1
E,20

CERIG,20,19,ALL
LSEL,S,LINE,,5,8,1
NSLL,S,1
NSEL,A,NODE,,20
CERIG,20,ALL,ALL
ALLSEL,ALL,ALL

! Bearings
ET,4,COMBIN14,,2  !1-D longitudinal spring-damper (UY degree of freedom)
ET,5,COMBIN14,,3  !1-D longitudinal spring-damper (UZ degree of freedom)
R,4,bstf

TYPE,4 
REAL,4
E,1,101 
E,8,110
TYPE,5 
REAL,4
E,1,201 
E,8,210

! Fix axial and rotational DOF at ends
D,1,UX 
D,1,ROTX
D,8,UX 
D,4,ROTX
! Fix Brng ground nodes
D,101,ALL 
D,201,ALL
D,110,ALL 
D,210,ALL
SAVE

! Plot the model
/view,1,1,1,1 $
/vup,1,z
/eshape,1,1 $
/pnum,sect,1
/num,1 $eplot

/COM,----------------------------------------------
/COM, Modal A\nalysis
/COM,----------------------------------------------
/TITLE, Tail rotor disk - Modal Analysis
/SOLU
! Setup modal run
ANTYPE,MODAL
CORIOLIS,ON,,,ON
MODOPT,QRDAMP,nmd,,,ON
! Loop on speeds
*DO,i,1,nmspn
	spn = (i-1)*(mxspn/(nmspn-1))
	OMEGA,spn
	MXPAND,nmd,,,YES
   SOLVE
*ENDDO
FINISH
SAVE


/POST1
! Plot/List Campbell Diagrams
/GROPTS,VIEW,1
/YRANGE,0,100
/XRANGE,0,spn
/VIEW,1,,,1 
/ANG,1  
/REP,FAST 
/SHOW,PNG
/GFILE,2400,
PLCAMP,,1,RPM
PRCAMP,,1,RPM
/SHOW, CLOSE

/VIEW,1,1,1,1   
/ANG,1  
/REP,FAST 
! Plot orbit
SET,3,1
PLORB
*USE, generateimages.mac
! Animate whirl
SET,3,1
PLNSOL,U,SUM
*USE, generateimages.mac
ANHARM

FINISH

/COM,-------------------------------
/COM,Transient Analisys
/COM,-------------------------------
/TITLE, Tail rotor disk - Transient Analisys
*SET, PI, ACOS(-1)
spin = spn*pi/30
tinc = 0.5e-3
tend = 4
spindot = spin/tend
nbp = nint(tend/tinc) + 1
unb = 1.e-4
f0 = unb*dskdia

*dim,spinTab,table,nbp,,,TIME
*dim,rotTab, table,nbp,,,TIME
*dim,fxTab,  table,nbp,,,TIME
*dim,fyTab,  table,nbp,,,TIME
*vfill,spinTab(1,0),ramp,0,tinc
*vfill,rotTab(1,0), ramp,0,tinc
*vfill,fxTab(1,0),  ramp,0,tinc
*vfill,fyTab(1,0),  ramp,0,tinc
tt = 0
*DO,iloop,1,nbp
   spinVal = spindot*tt
   spinTab(iloop,1) = spinVal
   spin2 = spinVal**2
   rotVal = spindot*tt**2/2
   rotTab(iloop,1) = rotVal
   sinr = sin(rotVal)
   cosr = cos(rotVal)
   fxTab(iloop,1)= f0*(-spin2*sinr + spindot*cosr)
   fyTab(iloop,1)= f0*( spin2*cosr + spindot*sinr)
   tt   = tt + tinc
*ENDDO
FINI

! ** transient analysis
/SOLU
ANTYPE,TRANSIENT

TIME,TEND
DELTIM,tinc,tinc/10,tinc*10
KBC,0
CORIOLIS,ON,,,ON
OMEGA,spin,,
F,5,FX,%fxTab%
F,5,FY,%fyTab%
OUTRES,ALL,ALL
SOLVE
FINI


! ** generate response graphs
/POST26
NSOL,2,5,U,X,UXdisk 
PROD,3,2,2  
NSOL,4,5,U,Y,UYdisk 
PROD,5,4,4  
ADD,6,3,5   
SQRT,7,6,,,Ampl_At_Disk
/AXLAB,Y,Displacement (m) 
/show,JPEG
plvar,7
EXTREME,7
/show,CLOSE

esol,8,4,5,smisc,32,Sy_At_Disk
esol,9,4,5,smisc,34,Sz_At_Disk
/axlab,y,Bending Stresses (N/m2)
/show,JPEG
plvar,8,9
EXTREME,8,9
/show,CLOSE
