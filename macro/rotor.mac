!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!                                                  !
!            TAIL ROTOR GENERATOR MACRO            !
!                                                  !
!                                                  !
!PARAMETERS INPUT:                                 !
!               ARG1: 1st node value               !
!               ARG2: 2nd node value               !
!		ARG3: pass distance between 	   !
!		      previous nodes		   !
!               ARG4: Local reference frame        !
!               ARG5: Local reference frame        !
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! >>>>> MODEL PARAMETERS SHAFT AND DISK <<<<<
*SET, shftlen, 0.50
*SET, shftdia, 0.05
*SET, dskoff, shftlen
*SET, brngoff, 0.12
*SET, dskdia, 1.912/2
*SET, d_i, 1.75/2
*SET, dskthk, 0.15
*SET, bstf, 378E+7 	! Brng Stiffness

/COM, -----------------------------------------------
/COM,  MODEL DEFINITION -SHAFT
/COM, -----------------------------------------------
!1st part
*GET, NodeMax, NODE, , NUM, MAX
*SET, NodeStart, NodeMax + 1
N, NodeStart,
N, NodeStart + 8, ARG3 $ /COM, %NodeStart + 8%, %ARG3%
*GET, NodeEnd, NODE, , NUM, MAX
FILL, NodeStart, NodeEnd

!2nd part
*GET, NodeMax, NODE, , NUM, MAX
N, NodeMax+19, dskoff
*GET, LastNode, NODE, , NUM, MAX
FILL, LastNode,  NodeEnd 
N, LastNode, nx(LastNode) + 0.02
*GET, LastNode, NODE, , NUM, MAX
SAVE

/COM, -----------------------------------------------
/COM,  MESH - SHAFT
/COM, -----------------------------------------------

! >>>>> MATERIAL PROPERTIES <<<<<
*GET, MaterialNum, MAT, , NUM, MAX

! Aluminum 7075
*SET, Aluminum7075, MaterialNum + 1
MP, EX, Aluminum7075, 71.7e+9
MP, DENS, Aluminum7075, 2810
MP, NUXY, Aluminum7075, 0.33

/COM, DEFINE BEAM188 for SHAFT
*GET, LastElementType, ETYP, , NUM, MAX
*SET, BeamShaft, LastElementType + 1
! Use 188 elements, solid
ET, BeamShaft, 188
KEYOPT, BeamShaft, 1, 1
KEYOPT, BeamShaft, 3, 2
KEYOPT, BeamShaft,15, 0

*GET, LastElementType, ETYP, , NUM, MAX
/COM, BEAM188 for SHAFT have number %BeamShaft%
*GET, LastSectionType, SECP, ,NUM, MAX
*SET, SectionBeamShaft, LastSectionType + 1
SECTYPE, SectionBeamShaft, BEAM, CSOLID
SECDATA, shftdia/2
!cm, BeamOmega, elem 

! Make elements
SECNUM, SectionBeamShaft
TYPE, BeamShaft
MAT, Aluminum7075
NSEL, S, NODE, , NodeStart, LastNode, 1
*GET, NodeNumShaft, NODE, , COUNT
*GET, NodeStartShaft, NODE, , NUM, MIN
*GET, NodeEndShaft, NODE, , NUM, MAX
*DO, i, NodeStartShaft, NodeEndShaft-1, 1
	E, i, i+1
*ENDDO
SAVE

/COM, -----------------------------------------------
/COM,  MODEL DEFINITION - DISK
/COM, -----------------------------------------------

! >>>> DEFINE GEOMETRY <<<<
WPCSYS, -1 
WPOFFS, nx(LastNode), ny(LastNode), nz(LastNode)
WPROTA, , , 90
CYL4, 0, 0, d_i/2, 0, dskdia/2, 360

/COM, -----------------------------------------------
/COM,  MESH - DISK
/COM, -----------------------------------------------
/COM, DEFINE SHELL181 for DISK
*GET, LastElementType, ETYP, , NUM, MAX
*SET, ShellDisk, LastElementType + 1
ET, ShellDisk, SHELL181
KEYOPT, ShellDisk, 3, 2
KEYOPT, ShellDisk, 8, 2

*GET, LastElementType, ETYP, , NUM, MAX
/COM, SHELL181 for Disk have number %ShellDisk%
*GET, LastSectionType, SECP, ,NUM, MAX
*SET, SectionShellDisk, LastSectionType + 1
SECTYPE, SectionShellDisk, SHELL
SECD, dskthk
TYPE, ShellDisk
SECNUM, SectionShellDisk
MAT, Aluminum7075
!cm, DiscOmega, elem
MSHKEY, 0
!MSHAPE, 1, 2D
ESIZE, 0.02
AMESH, ALL
SAVE

!*** clamp between disk and spindle

/COM, *** CLAMP BETWEEN DISK AND SPINDLE
/COM, DEFINE MASS21 for CLAMP BETWEEN DISK AND SPINDLE
*GET, LastElementType, ETYP, , NUM, MAX
*SET, ClampDiskSplinde, LastElementType + 1
ET, ClampDiskSplinde, MASS21
KEYOPT, ClampDiskSplinde, 2, 0
*GET, ClampDiskSplinde, ETYP, , NUM, MAX
/COM, MASS21 for clamp between disk and spindle have number %ClampDiskSplinde%
R, ClampDiskSplinde, 10, 10, 10, 8.59e-2, 4.295e-2, 4.295e-2
TYPE, ClampDiskSplinde
REAL, ClampDiskSplinde
MAT, Aluminum7075
E, LastNode
SAVE

CERIG, LastNode, LastNode-1, ALL
ALLSEL, ALL
LSEL, S, LOC, X, nx(LastNode)
LSEL, R, RADIUS, , d_i/2  
NSLL, S, 1
NSEL, A, NODE, , LastNode
CERIG, LastNode, ALL, ALL
/REP, FAST
SAVE

/COM, -----------------------------------------------
/COM,  MESH - BEARINGS
/COM, -----------------------------------------------
!*** define bearings
*GET, LastElementType, ETYP, , NUM, MAX
*SET, SpringDamper, LastElementType+1
ET, SpringDamper, COMBIN14
*GET, RealConstant, RCON, , NUM, MAX
*SET, SpringDumperRealConst, RealConstant + 1
R, SpringDumperRealConst, bstf

!1-D longitudinal spring-damper (UY degree of freedom)
TYPE, SpringDamper
KEYOPT, SpringDamper, 2, 2
REAL, SpringDumperRealConst
E, ARG1, NodeStart 
E, ARG2, NodeEnd
/COM, 1-D longitudinal spring-damper (UY degree of freedom): 
/COM,	Node1: %ARG1% (ARG1); Node2: %NodeStart% (NodeStart)
/COM,	Node1: %ARG2% (ARG2); Node2: %NodeEnd% (NodeEnd)

!***longitudinal spring-damper (UZ degree of freedom)
TYPE, SpringDamper
KEYOPT, SpringDamper, 2, 3 
E, ARG1, NodeStart
E, ARG2, NodeEnd
/COM, 1-D longitudinal spring-damper (UZ degree of freedom): 
/COM,	Node1: %ARG1% (ARG1); Node2: %NodeStart% (NodeStart)
/COM,	Node1: %ARG2% (ARG2); Node2: %NodeEnd% (NodeEnd)

/COM, -----------------------------------------------
/COM,  REVOLUTE JOINT
/COM,  create revolute joint to block d.o.f shaft
/COM, -----------------------------------------------
!MPC184 Revolute Joint Geometry shows the geometry and node locations for this element. 
!Two nodes (I and J) define the element. The two nodes are expected to have identical spatial 
!coordinates initially.
*GET, LastElementType, ETYP, , NUM, MAX
*SET, RevoluteJoint, LastElementType+1
ET, RevoluteJoint, MPC184
KEYOPT, RevoluteJoint, 1, 6

!If KEYOPT(4) = 0, then element is an x-axis revolute joint with the local e1 axis as the revolute axis.
!If KEYOPT(4) = 1, then element is a z-axis revolute joint with the local e3 axis as the revolute axis.
KEYOPT, RevoluteJoint, 4, 0

!***define revolute joint
TYPE, RevoluteJoint
SECTYPE, , JOINT, REVO, RevoJoint_1
SECJOINT, , ARG4, ARG4
*GET, LastSectionType, SECP, ,NUM, MAX 
SECNUM, LastSectionType
E, ARG1, NodeStart
SECTYPE, , JOINT, REVO, RevoJoint_2
SECJOINT, , ARG5, ARG5
*GET, LastSectionType, SECP, ,NUM, MAX 
SECNUM, LastSectionType
E, ARG2, NodeEnd
SAVE

!select element to
ESEL, S, TYPE, , BeamShaft
ESEL, A, TYPE, , ShellDisk
ESEL, A, TYPE, , ClampDiskSplinde
EPLOT

!create a component
CM, RotorBlock, ELEM

!in this case there is only one component
*GET, NumberComponet, 'COMP', , NCOMP
*GET, CM_Name, 'COMP', NumberComponet, NAME
*MSG, UI, NumberComponet, CM_Name
"Created: (%I) component, Named: (%S)"

!clear parameters
*SET, NodeMax, 
*SET, LastElementType,
*SET, RevoluteJoint,
*SET, LastNode,
*SET, LastElementType,
*SET, SpringDamper
*SET, SpringDumperRealConst
*SET, ClampDiskSplinde,
*SET, ShellDisk,
*SET, NodeNumShaft, 
*SET, NodeStartShaft, 
*SET, NodeEndShaft, 
*SET, BeamShaft,

!Exit Macro
/EOF