/COM, ---------------------------------------------------------------------------------
/COM, PROBLEM: MODEL of the TAIL ROTOR DISK
/COM, ...................................................................
/COM, cantilevered disk-spindle system
/COM, ---------------------------------------------------------------------------------

FINISH
/CLEAR, START, NEW
/FILNAM, Tail_Rotor_disk
/TITLE, Tail_Rotor_disk
*USE,testperson.mac


! >>>>> MODEL PARAMETERS <<<<<
shftlen = 0.50
shftdia = 0.05
dskoff = shftlen
brngoff = 0.12
dskdia = 1.912/2
d_i = 1.75/2
dskthk = 0.15
bstf = 378e+7 ! Brng Stiffness
nmd = 10      ! Number of modes
nmspn = 8     ! Number of speeds
mxspn = 2000  ! Max Omega (rpm)


/PREP7
! >>>>> MATERIAL PROPERTIES <<<<<
! Aluminum 7075
MP,EX,1,71.7e+9
MP,DENS,1,2810
MP,NUXY,1,0.33

! Build model nodes
n,1
n,8,brngoff
fill,1,8
n,19,dskoff
fill,8,19
N,20,nx(19) + 0.02


! Bearing ground nodes
n,101,
n,201,
n,110,nx(8)
n,210,nx(8)

! Use 188 elements, solid
et,1,188,,,2
sectype,1,beam,csolid
secdata,shftdia/2
! Make elements
secnum,1
type,1 $mat,1
real,1
e,1,2
egen,18,1,1

! Disk
CSYS,0
WPOFFS, nx(20), ny(20), nz(20)
WPROTA,,,90
/VIEW,1,1,1,1
CYL4,0,0,d_i/2,0,dskdia/2,360


ET,2,SHELL181
SECTYPE,2,SHELL
SECD,dskthk
TYPE,2
SECN,2
MAT,1
MSHKEY,0
ESIZE,0.02
AMESH,ALL


! ** clamp between disk and spindle
ET,3,MASS21
R,3,10,10,10,8.59E-2,4.295E-2,4.295E-2
TYPE,3
REAL,3
MAT,1
E,20

CERIG,20,19,ALL
LSEL,S,LINE,,5,8,1
NSLL,S,1
NSEL,A,NODE,,20
CERIG,20,ALL,ALL
ALLSEL,ALL,ALL




! Bearings
ET,4,COMBIN14,,2  !1-D longitudinal spring-damper (UY degree of freedom)
ET,5,COMBIN14,,3  !1-D longitudinal spring-damper (UZ degree of freedom)
R,4,bstf

type,4 $real,4
e,1,101 $e,8,110
type,5 $real,4
e,1,201 $e,8,210


! Fix axial and rotational DOF at ends
D,1,UX 
D,1,ROTX
D,8,UX 
D,4,ROTX
! Fix Brng ground nodes
D,101,ALL 
D,201,ALL
D,110,ALL 
D,210,ALL

SAVE


/eof

! Plot the model
/view,1,1,1,1 $
/vup,1,z
/eshape,1,1 $
/pnum,sect,1
/num,1 $eplot


/SOLU
! Setup modal run
ANTYPE,MODAL
CORIOLIS,ON,,,ON
MODOPT,QRDAMP,nmd,,,ON
! Loop on speeds
*DO,i,1,nmspn
	spn = (i-1)*(mxspn/(nmspn-1))
	OMEGA,spn
	MXPAND,nmd,,,YES
   SOLVE
*ENDDO
FINISH
SAVE


/POST1
! Plot/List Campbell Diagrams
/GROPTS,VIEW,1
/YRANGE,0,100
/XRANGE,0,4000
PLCAMP,,1,RPM
PRCAMP,,1,RPM

! Plot orbit
SET,5,6
PLORB
! Animate whirl
SET,5,6
PLNSOL,U,SUM
ANHARM

FINISH

