/COM,  ---------------------------------------------------------------------------------
/COM,   PROBLEM: MODAL ANALYSIS of the helicopter's TAIL BOOM
/COM,  ---------------------------------------------------------------------------------
/COM,   Natural frequencies and modal shapes of an helicopter's tail boom
/COM,  ---------------------------------------------------------------------------------

FINISH
/CLEAR, START, NEW
/FILNAM, Shellmodel
/TITLE, Helicopter tail boom modal analysis shell model
/UNIT, SI
/INQUIRE, StrJobname, JOBNAME
*USE, testperson.mac

! >>>>> MODEL PARAMETERS <<<<<
*SET, Pi, ACOS(-1)		!Pi constant
*AFUN, DEG     			!Specify units for angular measures [DEG], specify after function *AFUN
*SET, eps, 10e-3		!precision interval

! >>>>> MATERIAL PROPERTIES <<<<<
!*** Duraluminium
*SET, DuralEyounG, 72e9	 ![Pa] Young's modulus
*SET, DuralNi, 0.33		 !Poisson's ratio
*SET, DuralDensity, 2810 ![Kg/m^3]

!*** Aluminium
*SET, AluminiumEyounG, 64e9 	![Pa] Young's modulus
*SET, AluminiumNi, 0.34		!Poisson's ratio
*SET, AluminiumDensity, 2700 	![Kg/m^3]

!*** Element size
*SET, E_length_ct, 50e-3	![m]

!geometric parameter
*SET, Lt, 5.2			![m] total tail lenght
*SET, RadBaseTail, 0.65/2	![m]
*SET, RadEndTail, 0.10/2	![m]
*SET, alpha, atan((RadBaseTail-RadEndTail)/Lt) 	![deg]

!geometric parameters section stiffner
*SET, StiffnerBase, 5e-3
*SET, StiffnerHeight, 5e-3

!geometric parameters section Horizzontal Stabilizer
*SET, HorizStabBase, 0.009
*SET, HorizStabHeigth, 0.0015

!geometric parameter mantle
*SET, ThichknessMantle, 5e-4 			![m]

!other parameters
*SET, NumberDivisonSurface, 24	!division cone's area

/COM, -----------------------------------------------
/COM,  MODEL DEFINITION
/COM, -----------------------------------------------
! >>>> DEFINE GEOMETRY <<<<
! Reduced integration Timoshenko beam element, with quadratic shape functions
/PREP7
! Define axis rotation
K, 1, 0, 0, 0
K, 2, 5, 0, 0

! create keypoints
K, 3, 0, RadBaseTail, 0			!start keypoint tails
*GET, MaxKp, KP, 0, num, max 		!extract max keypoint
*SET, StartKpTail, MaxKp 		!store in variable
*DIM, SegmentRadius, ARRAY, 7, 1	!store in array radius of segment distance between 0 - Y before rotating
*DO, i, 1, 7, 1
	*GET, MaxKp, KP, 0, NUM, MAX 	!extract max keypoint
	K, MaxKp+1, Lt/7*i, RadBaseTail-Lt/7*i*tan(alpha), 0	!create keypoint
	SegmentRadius(i) = RadBaseTail-Lt/7*i*tan(alpha)
*ENDDO
*GET, MaxKp, KP, 0, num, max 		!extract max keypoint
*SET, EndKpTail, MaxKp			!store in variable end's tail

! define keypoint mantle'tail
K, Maxkp+1, 0, RadBaseTail, 0
*GET, StartKpMantle, KP, 0, num, max
K, MaxKp+2, Lt/7*i, RadEndTail, 0
*GET, EndKpMantle, KP, 0, num, max

*DO, j, StartKpTail, EndKpTail-1, 1
	L, j, j+1
*ENDDO
*GET, LenghtLineRference, LINE, 1, leng 	!extract lenght segment line's cone generator

! generate cone
*GET, NumberLine, line, 0, count 		!count lines
*GET, StartNumberLine, line, 0, num, min	!extract first number line
*GET, NumberLine, line, 0, count 		!count lines
*GET, StartNumberLine, line, 0, num, min	!extract first number line
LSEL, S, LOC, X, 0, Lt
AROTAT, ALL, , , , , , 1, 2, 360, NumberDivisonSurface		!generate cone

!divide line in array straight line and curved line
!to manage in next step meshing of elements
LSEL, S, LENGTH, , LenghtLineRference, LenghtLineRference
*VGET, StraighLine, LINE, , LLIST, , , 0
ALLSEL, ALL
LSEL, U, LENGTH, , LenghtLineRference, LenghtLineRference
*VGET, AcrLine, LINE, , LLIST, , , 0

*GET, StartNumberLine, line, 0, num, min
LSEL, ALL

/VIEW, 1, 1, 1, 1
/ANG, 1
/REP, FAST
LPLOT
*USE, generateimages.mac

! >>>> MESH <<<<
/ESHAPE, 1
!set material properties and element
ET, 1, BEAM189
MP, EX, 1, DuralEyounG
MP, NUXY, 1, DuralNi
MP, DENS, 1, DuralDensity

!***Horizzontal Stabilizer
SECTYPE,  1, BEAM, RECT, HorizzontalStabilizer, 0
SECOFFSET, CENT
SECDATA, HorizStabBase, HorizStabHeigth

*GET, LenghtStraighLine, 'PARM', StraighLine, DIM, X
LSEL, S, LINE, , StraighLine(1), StraighLine(LenghtStraighLine)
LESIZE, all, E_length_ct
LATT, 1, , 1, , , , 1
LMESH, ALL
LSEL, NONE
/ESHAPE, 1
/REPLO

!***stiffners
SECTYPE,  2, BEAM, RECT, Stiffeners, 0
SECOFFSET, ORIG
SECDATA, StiffnerBase, StiffnerHeight

*GET, LenghtAcrLine, 'PARM', AcrLine, DIM, X
LSEL, S, LINE, , AcrLine(1), AcrLine(LenghtAcrLine)
LESIZE, ALL, , , , 3
LATT, 1, , 1, , , , 2
LMESH, ALL
LSEL, NONE
/REPLO

/VIEW, 1, 1, 1, 1
/ANG, 1
/REP, FAST
EPLOT
*USE, generateimages.mac

!***Mantle
!set material properties and element
ET, 2, SHELL181
MP, EX, 2, AluminiumEyounG
MP, NUXY, 2, AluminiumNi
MP, DENS, 2, AluminiumDensity
SECT, 3, SHELL, , mantle
SECDATA, ThichknessMantle, 2, 0.0, 3
SECOFFSET, top

TYPE, 2
SECNUM, 3
AESIZE, ALL, E_length_ct,
MSHAPE, 0, 2D
MSHKEY, 1
AMESH, ALL
SAVE

/VIEW, 1, 1, 1, 1
/ANG, 1
/REP, FAST
EPLOT
*USE, generateimages.mac

!***Boundary conditions at base of tail
ALLSEL, ALL
LSEL, S, LOC, Y, -(RadBaseTail*2)+eps, (RadBaseTail*2)+eps
LSEL, S, LOC, x, 0, 0
DL, ALL, , ALL, 0
ALLSEL, ALL

!Selct keypoint-node to apply force
KSEL,S,LOC,X,4.457143
KSEL,R,LOC,Y,0.4464286E-01
KSEL,R,LOC,Z,-0.7732370E-01,0
*GET,ForceOnNode,KP,,NUM,MAX
FK,ForceOnNode,FZ,2243
ALLSEL, ALL
*USE, calcmass.mac, StrJobname(1)
FINISH

/COM, ------------------------------------------------
/COM,  Preliminary static analysis - Prestress
/COM, ------------------------------------------------
/SOLU
ANTYPE, STATIC		!Preliminary static analysis
PSTRES, ON			!Includes prestress effects
Acel, , 9.81			!gravitational acceleration
SOLVE
FINISH

! >>>> POSTPROCESSING <<<<
/POST1
PLDISP, 1
*USE, generateimages.mac
PLNSOL, U, SUM, 0, 1.0
*USE, generateimages.mac
PLNSOL, S, EQV, 0, 1.0
*USE, generateimages.mac
PLNSOL, U,Y, 0,1.0
*USE, generateimages.mac
PLNSOL, U,Z, 0,1.0
*USE, generateimages.mac
FINISH

/COM, ------------------------------------------------
/COM,  Modal analysis
/COM, ------------------------------------------------
! >>>> SOLUTION <<<<
/SOLU
ANTYPE, MODAL, NEW
PSTRES, ON			! Includes prestress effects
MODOPT, LANB, 10		! First 10 modes are extracted
LUMPM, OFF			! lumped/consistent mass matrix
MXPAND, 10, , , Yes		! First 10 modes are expanded, calculate element stress

SOLVE
FINISH

! >>>> POSTPROCESSING <<<<
/POST1
SET, LIST			!List natural frequencies
SET, 1, 1			!Loads first mode
PLDISP				!Displays modal shape

*DIM,ModalFreq,ARRAY,10,1
*Do, i, 1, 10, 1
 *GET, omega, MODE, i, FREQ
	ModalFreq(i,1) = omega
	*SET, omega,
*ENDDO

*SET, omega,
*CFOPEN, 'ModalFreq-%StrJobname(1)%', 'txt'
 *VWRITE, 'Num', 'omega'
  (8x, A8, 4X, A8,)
  *VWRITE, sequ, ModalFreq(1,1)
   (F12.0, F12.5)
*CFCLOS

FINISH
